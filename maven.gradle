apply plugin: 'maven'

// load properties
Properties properties = new Properties()
File file = project.rootProject.file("local.properties")
if (file.exists()) properties.load(file.newDataInputStream())
file = project.file("project.properties")
if (file.exists()) properties.load(file.newDataInputStream())
file = project.file("module.properties")
if (file.exists()) properties.load(file.newDataInputStream())

// read properties
String proName = properties.getProperty("project.name")
String proGroupId = properties.getProperty("project.groupId")
String proArtifactId = properties.getProperty("project.artifactId")
String proVersion = properties.getProperty("project.version")
String proSiteUrl = properties.getProperty("project.url.site")
String proGitUrl = properties.getProperty("project.url.git")
String proScmConnectionUrl = properties.getProperty("project.scm.connection")
String proScmDeveloperUrl = properties.getProperty("project.scm.developer")
String proDescription = properties.getProperty("project.description")
String proPkgExt = properties.getProperty("project.pkg.ext")
String proPkg = properties.getProperty("project.pkg")
if (proPkgExt == null || proPkgExt.length() == 0) {
    proPkgExt = "aar"
}

String devName = properties.getProperty("developer.name")
String devEmail = properties.getProperty("developer.email")
if (devName == null || devName.length() == 0) {
    devName = "sskbskdrin"
}
if (devEmail == null || devEmail.length() == 0) {
    devEmail = "keayuan_1991.com@163.com"
}

group = proGroupId

// This generates POM.xml with proper parameters

uploadArchives {
    configuration = configurations.archives
    repositories {
        mavenDeployer {
            repository(url: uri('../.repository'))
            pom.project {
                String count = properties.getProperty("dependencyCount")
                if (count != null) {
                    dependencies {
                        for (int i = Integer.parseInt(count); i > 0; --i) {
                            String dep = properties.getProperty("dependency-" + i)
                            String[] f = dep.split(":")
                            dependency {
                                groupId f[0]
                                artifactId f[1]
                                if (f.length > 2) version f[2]
                            }
                        }
                    }
                }
                packaging proPkgExt
                groupId proGroupId
                artifactId proArtifactId
                version proVersion
                name proName
                description proDescription
                url proSiteUrl
            }
        }
    }
}

// This generates sources.jar
task sourcesJar(type: Jar) {
    println("sourcesJar测试")
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    println("task javadoc")
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version proVersion
        links "http://docs.oracle.com/javase/8/docs/api"
        title proName
    }
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    println("javadocJar")
    classifier = 'javadoc'
    from javadoc.destinationDir
}

if (proPkgExt == 'jar') {
    task makeJar(type: Jar) {
        println("makeJar")
        delete 'build/libs/'
        from("build/intermediates/javac/release/classes/$proPkg")
        into proPkg
        exclude("BuildConfig.class", "R.class")
    }
//    makeJar.dependsOn(build)
}

artifacts {
    println("artifacts")
    archives sourcesJar
    archives javadocJar
    if (proPkgExt == 'jar') {
        archives makeJar
    }
}
